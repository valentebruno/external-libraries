#
# qmake configuration for 64-bit Leap Dev boards
#
# A typical configure line might look like:
# configure \
#   -device leap-lmvp \
#   -device-option CROSS_COMPILE=/usr/bin/aarch64-gnu-linux- \
#   -device-option COMPILE_VERSION=4.9
#   -no-gcc-sysroot \
#   -opengl es2

# Note: This enables eglfs and wayland only. To enable xcb (with EGL
# support) as well, add -qt-xcb and fix the SDK's X11 headers. See
# QTBUG-55140.

include(../common/linux_device_pre.conf)

QMAKE_CC                = $${CROSS_COMPILE}gcc-$${COMPILE_VERSION}
QMAKE_CXX               = $${CROSS_COMPILE}g++-$${COMPILE_VERSION}
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

QMAKE_INCDIR += \
    $$[QT_SYSROOT]/usr/include

QMAKE_LIBDIR += \
    $$[QT_SYSROOT]/usr/lib \
    $$[QT_SYSROOT]/lib/aarch64-linux-gnu \
    $$[QT_SYSROOT]/usr/lib/aarch64-linux-gnu

QMAKE_LFLAGS += \
    -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib \
    -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib/aarch64-linux-gnu \
    -Wl,-rpath-link,$$[QT_SYSROOT]/lib/aarch64-linux-gnu

DISTRO_OPTS                  += aarch64

# Do not define WIN_INTERFACE_CUSTOM here. It is suitable for drm and
# wayland, but not X11. Leave it to qt_egl_p.h instead.
# COMPILER_FLAGS               += -mtune=cortex-a57.cortex-a53 -march=armv8-a

# EGLFS_DEVICE_INTEGRATION = eglfs_kms_egldevice

include(../common/linux_device_post.conf)
load(qt_config)
